plugins {
    id 'nebula.release' version '4.1.0'
    id 'maven'
}
nebulaRelease { 
    // need this for gitflow release names
    addReleaseBranchPattern(/release\/\d+(\.\d+)+/)
}

project(":modules:Base") {
    apply plugin: "module"
}
project(":modules:Extras") {
    apply plugin: "module"
}
project(":services:Nifi") {
    apply plugin: "service"
}
project(":services:Zookeeper") {
    apply plugin: "service"
}

configurations {
    moduleArchives
    serviceArchives
}

dependencies {
    moduleArchives project(path: ":modules:Base", configuration: "archives")
    moduleArchives project(path: ":modules:Extras", configuration: "archives")
    serviceArchives project(path: ":services:Nifi", configuration: "archives")
    serviceArchives project(path: ":services:Zookeeper", configuration: "archives")
}

task test {}

task "package" (type: Tar) {
    dependsOn configurations.serviceArchives, configurations.moduleArchives

    baseName project.name
    version project.version.toString()
    destinationDir project.buildDir
    compression Compression.GZIP
    extension "tar.gz"

    into("${baseName}-${version}/services") {
        from {
            configurations.serviceArchives.collect { tarTree(it) }
        }
    }

    into("${baseName}-${version}/modules") {
        from {
            configurations.moduleArchives.collect { tarTree(it) }
        }
    }
}

artifacts {
    archives(package)
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
