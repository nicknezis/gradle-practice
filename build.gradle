plugins {
    id 'nebula.release' version '4.1.0'
}
nebulaRelease { 
    addReleaseBranchPattern(/release\/\d+(\.\d+)+/)
}

project(":modules:Base") {
    apply plugin: "module"
}
project(":modules:Extras") {
    apply plugin: "module"
}
project(":services:Nifi") {
    apply plugin: "service"
}
project(":services:Zookeeper") {
    apply plugin: "service"
}

configurations {
    moduleArchives
    serviceArchives
}

dependencies {
    moduleArchives project(path: ":modules:Base", configuration: "archives")
    moduleArchives project(path: ":modules:Extras", configuration: "archives")
    serviceArchives project(path: ":services:Nifi", configuration: "archives")
    serviceArchives project(path: ":services:Zookeeper", configuration: "archives")
}

task packageModules(type: Copy) {
    into "${buildDir}/modules"
    from {
        configurations.moduleArchives
    }
}

task packageServices(type: Copy) {
    into "${buildDir}/services"
    from {
        configurations.serviceArchives
    }
}

task "package" {
    dependsOn packageModules, packageServices
}

task clean(type: Delete) {
    delete buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
