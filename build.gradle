plugins {
    id 'nebula.release' version '4.1.0'
    id 'nebula.os-package' version '2.2.6'
    id 'maven'
}
nebulaRelease { 
    // need this for gitflow release names
    addReleaseBranchPattern(/release\/\d+(\.\d+)+/)
}

project(":modules:Base") {
    apply plugin: "module"
}
project(":modules:Extras") {
    apply plugin: "module"
}
project(":services:Nifi") {
    apply plugin: "service"
}
project(":services:Zookeeper") {
    apply plugin: "service"
}

configurations {
    moduleArchives
    serviceArchives
}

dependencies {
    moduleArchives project(path: ":modules:Base", configuration: "archives")
    moduleArchives project(path: ":modules:Extras", configuration: "archives")
    serviceArchives project(path: ":services:Nifi", configuration: "archives")
    serviceArchives project(path: ":services:Zookeeper", configuration: "archives")
}

task test {}

buildRpm {
    dependsOn configurations.serviceArchives, configurations.moduleArchives

    version = project.version.toString().replaceAll("-", "_")
    release "1"

    into("/c2") {
        from {
            configurations.serviceArchives.collect { tarTree(it) }
        }
        addParentDirs false
    }

    into("/c2/install.d") {
        from {
            configurations.moduleArchives.collect { tarTree(it) }
        }
        addParentDirs false
        createDirectoryEntry true
    }
}

artifacts {
    archives buildRpm
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
